package UAV
public
	
	with MC;
	with FC;
	with UAS_Buses;
	with HAMR;
	
	-- UAV
	system UAV
		features
			radio_recv: in event data port;
			radio_send: out event data port;
			RFA: requires bus access UAS_Buses::RF_Bus.Impl;
	end UAV;
	
	system implementation UAV.Impl
		subcomponents
			MCMP: system MC::MissionComputer.Impl;
			FCTL: system FC::FlightController.Impl;
			FCTL_SBUS: bus UAS_Buses::Serial_Bus.Impl;
		connections
			
			-- FC UART bus access
			bac1: bus access FCTL_SBUS <-> MCMP.FC_UART;
			bac2: bus access FCTL_SBUS <-> FCTL.UART;
					
			-- Radio bus access
			bac5: bus access RFA -> MCMP.RFA;
			
			-- Radio
			c1: port radio_recv -> MCMP.radio_recv;
			c2: port MCMP.radio_send -> radio_send;
			
			-- FC UART
			c3: port MCMP.fc_uart_send -> FCTL.uart_recv;
			c4: port FCTL.uart_send -> MCMP.fc_uart_recv;
				
		properties
			Actual_Connection_Binding => (reference (FCTL_SBUS)) applies to c3, c4;
			
			HAMR::Default_Bit_Width => 32;
            HAMR::Default_Max_Sequence_Size => 1;
            HAMR::Max_String_Size => 256;
            HAMR::Platform => (Linux, SeL4);
            HAMR::Bit_Codec_Raw_Connections => true;  
	end UAV.Impl;
	
end UAV;