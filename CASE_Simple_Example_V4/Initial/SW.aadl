package SW
public

	with Data_Model;
	with Base_Types;
	with CASE_Properties;
	with HAMR;

	data KeepInZone
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Coordinate.Impl));
			Data_Model::Dimension => (2);
			HAMR::Bit_Codec_Max_Size => 24 Bytes;
	end KeepInZone;
	
	data implementation KeepInZone.Impl		
	end KeepInZone.Impl;

	data Coordinate
	end Coordinate;

	data implementation Coordinate.Impl
		-- This is the structure to hold the lat/long/alt values of a coordinate
		subcomponents
			Latitude: data Base_Types::Float_32;
			Longitude: data Base_Types::Float_32;
			Altitude: data Base_Types::Float_32;
		properties
			HAMR::Bit_Codec_Max_Size => 12 Bytes;
	end Coordinate.Impl;

	data Map
		-- The Map is a structure that contains 2 coordinate on either side of a region.
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Coordinate.Impl));
			Data_Model::Dimension => (2);
			HAMR::Bit_Codec_Max_Size => 24 Bytes;
	end Map;

	data Command
	end Command;

	data implementation Command.Impl
		-- The Command structure contains data that the Ground Station passes to the UAV.
		subcomponents
			Map: data Map;
		properties
			HAMR::Bit_Codec_Max_Size => 24 Bytes;
	end Command.Impl;

	-- Message header
	data MsgHeader
	end MsgHeader;

	data implementation MsgHeader.Impl
		subcomponents
			src: data Base_Types::Unsigned_32;
			dst: data Base_Types::Unsigned_32;
		properties
			HAMR::Bit_Codec_Max_Size => 8 Bytes;
	end MsgHeader.Impl;

	-- RF Message structure
	data RF_Msg
	end RF_Msg;

	data implementation RF_Msg.Impl
		subcomponents
			header: data MsgHeader.Impl;
			payload: data Command.Impl;
		properties
			HAMR::Bit_Codec_Max_Size => 32 Bytes;
	end RF_Msg.Impl;
	
	-- This is the structure of an Attestation Request message
	-- that the Attestation Manager sends to the comm driver
	data AttestationRequestMsg
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Base_Types::Unsigned_8));
			Data_Model::Dimension => (16);
			HAMR::Bit_Codec_Max_Size => 16 Bytes;
	end AttestationRequestMsg;

	data implementation AttestationRequestMsg.Impl
	end AttestationRequestMsg.Impl;

	-- This is the structure of an Attestation Response message
	-- that the comm driver returns to the Attestation Manager
	-- when it gets a response from the Ground Station
	data AttestationResponseMsg
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Base_Types::Unsigned_8));
			Data_Model::Dimension => (2048);
			HAMR::Bit_Codec_Max_Size => 2048 Bytes;
	end AttestationResponseMsg;

	data implementation AttestationResponseMsg.Impl
	end AttestationResponseMsg.Impl;

	data AddressArray
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Base_Types::Unsigned_32));
			Data_Model::Dimension => (4);
			HAMR::Bit_Codec_Max_Size => 16 Bytes;
	end AddressArray;

	data implementation AddressArray.Impl
	end AddressArray.Impl;

	data AllowList	
	end AllowList;
	
	data implementation AllowList.Impl
		subcomponents
			value: data AddressArray.Impl;
		properties
			HAMR::Bit_Codec_Max_Size => 16 Bytes;
	end AllowList.Impl;

	data Mission
		-- The Mission is a list of waypoints that is generated by the Flight Planner based on a Map
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Coordinate.Impl));
			Data_Model::Dimension => (3);
			HAMR::Bit_Codec_Max_Size => 36 Bytes;
	end Mission;
	
	thread AttestationTester
		features
			AttestationRequest: in event data port AttestationRequestMsg.Impl;
			AttestationResponse: out event data port AttestationResponseMsg.Impl;
		properties
			CASE_Properties::Attesting => 100;
	end AttestationTester;
	
	thread implementation AttestationTester.Impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
			CASE_Properties::Component_Language => CakeML;
	end AttestationTester.Impl;

	thread RadioDriver
		-- The RadioDriver is the software that enables other SW components to communicate to other systems over RF
		features
			AttestationTesterResponse: in event data port AttestationResponseMsg.Impl;
			AttestationTesterRequest: out event data port AttestationRequestMsg.Impl;
			MissionCommand: out event data port RF_Msg.Impl;
		properties
			CASE_Properties::Comm_Driver => true;
		annex agree {**
			guarantee Req001_RadioDriver "Only valid command messages shall be forwarded to message destination components" : VALID_MESSAGE(MissionCommand);
		**};
	end RadioDriver;

	thread implementation RadioDriver.Impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
	end RadioDriver.Impl;

	thread FlightPlanner
		-- The FlightPlanner accepts a command message containing a map and flight pattern, and generates a mission.
		features
			MissionCommand: in event data port RF_Msg.Impl;
			FlightPlan: out event data port Mission;
		annex agree {**
			assume Req001_FlightPlanner "The Flight Planner shall receive a valid message from the Ground Station" : VALID_MESSAGE(MissionCommand);
			guarantee Req002_FlightPlanner "The Flight Planner shall generate a valid mission" : good_mission(FlightPlan);
		**};
	end FlightPlanner;

	thread implementation FlightPlanner.Impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
	end FlightPlanner.Impl;

	thread FlightController
		-- The FlightController is the software that flies the UAV
		features
			Alert: in event port;
		FlightPlan: in event data port Mission;
			annex agree {**
			assume Req001_FlightController "Well-formed mission window" : good_mission(FlightPlan);
		**};
	end FlightController;

	thread implementation FlightController.Impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
	end FlightController.Impl;

	process SW
	end SW;

	process implementation SW.Impl
		subcomponents
			AttestationTester: thread AttestationTester.Impl;
			Radio: thread RadioDriver.Impl;
			FlightPlanner: thread FlightPlanner.Impl;
			FlightController: thread FlightController.Impl;
		connections
			c1: port Radio.AttestationTesterRequest -> AttestationTester.AttestationRequest;
			c2: port AttestationTester.AttestationResponse -> Radio.AttestationTesterResponse;
			c3: port Radio.MissionCommand -> FlightPlanner.MissionCommand;
			c4: port FlightPlanner.FlightPlan -> FlightController.FlightPlan;
	end SW.Impl;

	annex agree {**
		-- ID of this UAV
		const CASE_UAV_ID : int = 42;
		fun VALID_MESSAGE(msg : RF_Msg.Impl) : bool = (msg.header.src > 0 and msg.header.dst = CASE_UAV_ID);
		fun WELL_FORMED_MESSAGE(msg : RF_Msg.Impl) : bool = good_command(msg.payload);
		-- These functions check the well-formedness of message structures
		fun good_coordinate(coord : Coordinate.Impl) : bool = coord.Latitude >= -90.0 and coord.Latitude <= 90.0 and coord.Longitude >= -180.0 and coord.Longitude <= 180.0 and coord.Altitude >= 0.0 and coord.Altitude <= 15000.0;
		fun good_map(map : Map) : bool = forall wp in map, good_coordinate(wp);
		fun good_command(cmd : Command.Impl) : bool = good_map(cmd.Map);
		fun good_mission(mission : Mission) : bool = forall wp in mission, good_coordinate(wp);
	**};
	
	process AttestationTester_seL4
		features
			AttestationRequest: in event data port AttestationRequestMsg.Impl;
			AttestationResponse: out event data port AttestationResponseMsg.Impl;
		properties
			CASE_Properties::Attesting => 100;
	end AttestationTester_seL4;

	process implementation AttestationTester_seL4.Impl
		subcomponents
			AttestationTester: thread AttestationTester.Impl;
		connections
			c1: port AttestationTester.AttestationResponse -> AttestationResponse;
			c2: port AttestationRequest -> AttestationTester.AttestationRequest;
		properties
			Period => 500ms;
			CASE_Properties::Component_Language => CakeML;
			CASE_Scheduling::Domain => 2;
	end AttestationTester_seL4.Impl;

	process RadioDriver_seL4
		features
			AttestationTesterResponse: in event data port AttestationResponseMsg.Impl;
			AttestationTesterRequest: out event data port AttestationRequestMsg.Impl;
			MissionCommand: out event data port RF_Msg.Impl;
		properties
			CASE_Properties::Comm_Driver => true;
	end RadioDriver_seL4;

	process implementation RadioDriver_seL4.Impl
		subcomponents
			RadioDriver: thread RadioDriver.Impl;
		connections
			c1: port AttestationTesterResponse -> RadioDriver.AttestationTesterResponse;
			c2: port RadioDriver.AttestationTesterRequest -> AttestationTesterRequest;
			c3: port RadioDriver.MissionCommand -> MissionCommand;
		properties
			Period => 500ms;
			CASE_Scheduling::Domain => 3;
		annex agree {**
			lift contract;
		**};
	end RadioDriver_seL4.Impl;

	process FlightPlanner_seL4
		features
			MissionCommand: in event data port RF_Msg.Impl;
			FlightPlan: out event data port Mission;
	end FlightPlanner_seL4;

	process implementation FlightPlanner_seL4.Impl
		subcomponents
			FlightPlanner: thread FlightPlanner.Impl;
		connections
			c1: port FlightPlanner.FlightPlan -> FlightPlan;
			c2: port MissionCommand -> FlightPlanner.MissionCommand;
		properties
			Period => 500ms;
			CASE_Scheduling::Domain => 4;
		annex agree {**
			lift contract;
		**};
	end FlightPlanner_seL4.Impl;

	process FlightController_seL4
		features
			Alert: in event port;
			FlightPlan: in event data port Mission;
	end FlightController_seL4;

	process implementation FlightController_seL4.Impl
		subcomponents
			FlightController: thread FlightController.Impl;
		connections
			c1: port Alert -> FlightController.Alert;
			c2: port FlightPlan -> FlightController.FlightPlan;
		properties
			Period => 500ms;
			CASE_Scheduling::Domain => 5;
		annex agree {**
			lift contract;
		**};
	end FlightController_seL4.Impl;

	system SW_seL4
	end SW_seL4;

	system implementation SW_seL4.Impl
		subcomponents
			FlightPlanner: process FlightPlanner_seL4.Impl;
			FlightController: process FlightController_seL4.Impl;
			AttestationTester: process AttestationTester_seL4.Impl;
			Radio: process RadioDriver_seL4.Impl;
		connections
			c1: port Radio.AttestationTesterRequest -> AttestationTester.AttestationRequest;
			c2: port AttestationTester.AttestationResponse -> Radio.AttestationTesterResponse;
			c3: port Radio.MissionCommand -> FlightPlanner.MissionCommand;
			c4: port FlightPlanner.FlightPlan -> FlightController.FlightPlan;
	end SW_seL4.Impl;
end SW;